alias sensor d0
alias combustor d1
alias fuelMixer d2
alias waterTank d3
alias waterFilter d4
alias steamCoolingVent d5

define stirlingEngines -260316435
# set max temp to 135C in Kelvin
define maxOperatingTemperature 408
define maxVolumeStored 100
define maxPressure 5000
define fuelMixerInput1 85 # 90% atmo + 10% oxygen

alias runSystem r0
alias outsideTemp r1
alias waterTankVolume r2
alias tempOK r3
alias volumeOK r4
alias filtersOK r5
alias filterPresent0 r6
alias filterPresent1 r7
alias filterStatus0 r8
alias filterStatus1 r9

yield
jal setup

mainLoop:
    yield
    jal checkRunConditions
    jal doRunSystem
    j mainLoop

setup:
    move runSystem 0
    s steamCoolingVent Mode 1
    s steamCoolingVent Lock 1
    s fuelMixer Setting fuelMixerInput1
    s fuelMixer Lock 1
    # disable all components
    s steamCoolingVent On 0 
    s waterFilter On 1
    sb stirlingEngines On 0
    s combustor On 0
    s fuelMixer On 0
    j ra

doRunSystem:
    s steamCoolingVent On runSystem
    sb stirlingEngines On runSystem
    s waterFilter Mode runSystem
    s combustor On runSystem
    s fuelMixer On runSystem
    j ra
    

checkRunConditions:
    # check temps and water volume
    l outsideTemp sensor Temperature
    l waterTankVolume waterTank VolumeOfLiquid
    s db Setting waterTankVolume
    # check filters and their status
    ls filterPresent0 waterFilter 1 Occupied
    ls filterPresent1 waterFilter 2 Occupied
    ls filterStatus0 waterFilter 1 Quantity
    ls filterStatus1 waterFilter 2 Quantity
    or filtersOK filterPresent0 filterPresent1
    sgt filterStatus0 filterStatus0 0
    sgt filterStatus1 filterStatus1 0
    or filterStatus0 filterStatus0 filterStatus1
    and filtersOK filtersOK filterStatus0

    # check if temp is below max temp
    sle tempOK outsideTemp maxOperatingTemperature
    sle volumeOK waterTankVolume maxVolumeStored
    # run system if temp and volume and filters are ok
    # s db Setting tempOK
    # s db Setting volumeOK
    and runSystem tempOK volumeOK
    and runSystem runSystem filtersOK
    # s db Setting runSystem
    j ra