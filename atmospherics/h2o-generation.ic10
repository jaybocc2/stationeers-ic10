alias waterTank d0
alias filterWarningLed d1
alias systemRunningLed d2

define stirlingEngines -260316435
define steamCoolingVent -1129453144
define gasSensor -1252983604
define combustor 1840108251
define fuelMixer 2104106366
define filterUnit -348054045
define purgeValve -737232128
define maxOperatingTemperature 410 # set max temp to 135C in Kelvin
define maxVolumeStored 100
define maxPressure 9000
define fuelMixerInput1 25 # 90% atmo + 10% oxygen # for filtered volatiles and 25* O2
define purgeValveSetting 10

alias runSystem r0
alias outsideTemp r1
alias waterTankVolume r2
alias tempOK r3
alias volumeOK r4
alias filtersOK r5
alias needReplaceFilters r6
alias combustionPressureOK r7
alias combustorOutputPressure r8
alias runCombustion r9
alias unitFiltersStatus r10
alias unitFiltersPresent r11
alias slotStatus r12


yield
jal setup

mainLoop:
    yield
    jal checkRunConditions
    jal doRunSystem
    j mainLoop

setup:
    move runSystem 0
    move needReplaceFilters 0
    move runCombustion 0
    move combustionPressureOK 0
    sb steamCoolingVent Mode 1
    sb steamCoolingVent Lock 1
    sb fuelMixer Setting fuelMixerInput1
    sb fuelMixer Lock 1
    sb purgeValve Lock 1
    sb purgeValve Setting purgeValveSetting
    s filterWarningLed Color 4
    s filterWarningLed Lock 1
    s systemRunningLed Color 2
    s systemRunningLed Lock 1
    sb filterUnit On 1
    j ra

doRunSystem:
    sb steamCoolingVent On runSystem
    sb stirlingEngines On runSystem
    sb filterUnit Mode runSystem
    sb purgeValve On runSystem
    s filterWarningLed On needReplaceFilters
    s systemRunningLed On runSystem
    j ra

doRunCombustion:
    lb combustorOutputPressure combustor PressureOutput Average
    sle combustionPressureOK combustorOutputPressure maxPressure
    and runCombustion runSystem combustionPressureOK
    sb combustor On runCombustion
    sb fuelMixer On runCombustion
    j ra

checkRunConditions:
    # Assume filters are OK, will be set to 0 if any fail
    move filtersOK 1
    move needReplaceFilters 0

    push ra
    jal checkFilters
    jal checkOtherConditions
    pop ra
    j ra

checkFilters:
    move unitFiltersStatus 0
    lbs slotStatus filterUnit 0 Quantity Minimum
    sgtz filtersOK slotStatus
    seqz needReplaceFilters filtersOK
    beqz filtersOK ra
    lbs slotStatus filterUnit 1 Quantity Minimum
    sgtz filtersOK slotStatus
    seqz needReplaceFilters filtersOK
    j ra

checkOtherConditions:
    lb outsideTemp gasSensor Temperature Average
    l waterTankVolume waterTank VolumeOfLiquid
    sle tempOK outsideTemp maxOperatingTemperature
    sle volumeOK waterTankVolume maxVolumeStored
    # Combine temp and volume checks
    and runSystem tempOK volumeOK
    # AND with the final filter status
    and runSystem runSystem filtersOK
    # Set if any filter is depleted
    seqz needReplaceFilters filtersOK
    j ra